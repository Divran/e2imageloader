@name imageloader-loadshare

#Source https://github.com/Divran/e2imageloader

@persist ImageData:string
@persist LoadShare_Start LoadShare_Stop Step NumScreenSqrt
@persist LinkedDigis:array ImageSize:vector2 MaxRes:vector2
@persist G:gtable

if (first()) {
    G = gTable("imageloader",0)    
    
    dsSetScope(0)
    dsJoinGroup("imageloader")
    dsSend("register","imageloader-main","derp")
} elseif (dsClk("stop")) {
    stoptimer("step")
} elseif (dsClk("load")) {

    local Data = dsGetTable()

    LinkedDigis = Data["wirelink",array]
    NumScreenSqrt = sqrt(LinkedDigis:count())
    ImageData = Data["imagedata",string]
    ImageSize = Data["imagesize",vector2]
    MaxRes = Data["maxres",vector2]
    LoadShare_Start = Data["start",number]
    LoadShare_Stop = Data["stop",number]
    Step = floor(LoadShare_Start / 9)
    
    timer("step",1)
} elseif (clk("step")) {
    while(perf()) {
        local Color = ImageData:sub(Step*9+1,Step*9+9):toNumber()
        if (Color != 0) {
            local X = Step % ImageSize:x()
            local Y = floor(Step / ImageSize:y())
            
            local DigiX = floor(X / 512)
            local DigiY = floor(Y / 512) * NumScreenSqrt
            
            LinkedDigis[DigiX+DigiY+1,wirelink][X % 512 + 512*(Y % 512)] = Color
        }
        Step++
    }
    
    if (G["stop",number] == 0 & Step*9+9 < LoadShare_Stop) {
        timer("step",10)
    }
}
