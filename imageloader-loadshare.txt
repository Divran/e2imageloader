@name imageloader-loadshare

#Source https://github.com/Divran/e2imageloader

@persist ImageData:string
@persist LoadShare_Start LoadShare_Stop Step NumScreenSqrt
@persist LinkedDigis:array ImageSize:vector2 MaxRes:vector2
@persist G:gtable DataSize ScreenSize:vector2

if (first()) {
    G = gTable("imageloader",0)    
    
    dsSetScope(0)
    dsJoinGroup("imageloader")
    dsSend("register","imageloader-main","derp")
    DataSize = 6
} elseif (dsClk("stop")) {
    stoptimer("step")
} elseif (dsClk("load")) {

    local Data = dsGetTable()

    LinkedDigis = Data["wirelink",array]
    NumScreenSqrt = sqrt(LinkedDigis:count())
    ImageData = Data["imagedata",string]
    ImageSize = Data["imagesize",vector2]
    MaxRes = Data["maxres",vector2]
    LoadShare_Start = Data["start",number]
    LoadShare_Stop = Data["stop",number]
    ScreenSize = Data["screensize",vector2]
    Step = LoadShare_Start
    
    timer("step",1)
} elseif (clk("step")) {
    while(perf()) {
        local Color = ImageData:sub(Step*DataSize+1,Step*DataSize+DataSize):toNumber(16)
        if (Color != 0) {
            if (NumScreenSqrt == 1) {
                LinkedDigis[1,wirelink][Step] = Color
            } else {
                local X = Step % ImageSize:x()
                local Y = floor(Step / ImageSize:y())
                
                local DigiX = floor(X / ScreenSize:x())
                local DigiY = floor(Y / ScreenSize:y()) * NumScreenSqrt
                
                LinkedDigis[DigiX+DigiY+1,wirelink][X % ScreenSize:x() + ScreenSize:x() * (Y % ScreenSize:y())] = Color
            }
        }
        Step++
    }
    
    if (G["stop",number] == 0 & Step < LoadShare_Stop) {
        timer("step",10)
    }
}
